---
layout: post
title: "lex和yacc"
date: "2022-08-03"
categories: 
  - "c-计算机"
---

# yacc

yacc分三部分

- 定义部分
- 规则部分
- 函数部分

```
[第一部分：定义段]

%%

第二部分：规则段

[%%

第三部分：辅助函数段]
```

 

%token 定义了哪些终结符。

 

结合方式

- %left ： 左结合
- %right ： 右结合
- %nonassoc ： 不可结合（即它定义的终结符不能连续出现：例如<，如果文法中不允许出现形如a<b<c的句子，则<就是不可结合的）

而优先级关系则是以他们定义出现的顺序决定的，先定义的优先级低，最后定义的优先级最高，同时定义的优先级相同

%start指定文法的开始符号（非终结符），定义形式为： %start startsym ，其中startsym为文法的开始符号。如果不使用%start定义文法开始符号，则默认在第二部分规则段中定义的第一条产生式规则的左部非终结符为开始符号。

%union和%type用来处理文法中各符号所带的属性。在词法分析的学习中，我们知道记号是由记号名和记号的属性值两部分组成的，文法中的终结符就是记号，他们有属性值，同样，非终结符也是可以有属性值的。

如果你想让属性值栈可以存放多种类型的属性值，例如整型和字符串型等（这在很多情况下是需要的，比如你希望标识符ID的属性是字符串而整型数NUM的属性是整型值），你最好将属性值栈元素的类型定义为一种union类型，此时，你可以用%union来定义它。例如，如下这样的定义会将Yacc属性值栈元素的类型定义为包含num和id两个域的联合体，其中num域的类型为int而id域的类型为char\*。

```
%union{
   int num;
   char * id;
}
```

对于终结符类型可以这样定义

```
%token <num> TOKEN1
%token <id> TOKEN2
```

 

%prec : S 用于为规则命名，这个名称被优先级定义引用

```
"-" expr %prec UMINUS
```

 

 

lex 内部预定义变量和函数

- ytext char \* 当前匹配的字符串
- yleng int 当前匹配的字符串的长度
- yin FILE \* lex当前的解析文件，默认为标准输入
- yout FILE \* lex 解析后的输出文件，默认为标准输出
- ylineno int 当前的行数信息
- yylex(void) 调用lex进行词法分析
- yywrap(void) 在文件（或输入）的末尾调用，如果函数的返回值是1，就停止解析。

 

# 例子

## 四则运算

calc.l

```
%{
    #include <stdlib.h>
    #include <string.h>
    void yyerror(char *);
    #include "calc.tab.h"
%}

%%
[0-9]+|[0-9]+"."[0-9]* {yylval.floatval =atof(yytext); return NUM;}
"+" {return OP_ADD;}
"-" {return OP_SUB;}
"**" {return OP_POW;}
"*" {return OP_MUL;}
"/" {return OP_DIV;}
"(" {return LEFT_BRACKET;}
")" {return RIGHT_BRACKET;}


[a-z]+ {yylval.id=strdup(yytext);return OP_FUN;}


"\n" {return END;} /* 回车*/
"\0" {return END;}

[\t ] ;
 
. yyerror("无效字符");

%%

int yywrap(void){
    return 1;
}
```

calc.y

```
%{
    #include <stdio.h>
    #include <stdlib.h>
    #include <math.h>
    #include <string.h>
    #define PI 3.14159265
    int yylex(void);
    void yyerror(char *);

    /*** 如下的是可以设置成输入字符串的。  **/
    typedef struct yy_buffer_state * YY_BUFFER_STATE;
    extern int yyparse();
    extern YY_BUFFER_STATE yy_scan_string(char * str);
    extern void yy_delete_buffer(YY_BUFFER_STATE buffer);

    double result; // 输出的结果。

%}

// 有如下的标识符
%token NUM OP_ADD OP_SUB OP_MUL OP_DIV END NEG OP_POW OP_FUN LEFT_BRACKET RIGHT_BRACKET
// 优先级和结核性如下。
%left OP_ADD OP_SUB 
%left OP_MUL OP_DIV 
%right OP_POW
%left OP_FUN
%left NEG

// 数据类型。有两种，一种是浮点数，而另一种是字符串（函数名）
%union{
    double floatval;
    char * id;
}

// 如下的是数据类型，大多数类型都是数字（浮点数），只有OP_FUN是字符串。
%type <floatval> expr term factor fun NUM 
%type <id> OP_FUN

%%

line:
    line expr END {
        //printf("%2f\n", $2);
        result = $2;
        }
    |;
;
expr:
    expr OP_ADD term {$$ = $1 + $3;}
    | expr OP_SUB term {$$ = $1 - $3;}
    | term {$$=$1;}
;
term:
    term OP_MUL factor {$$ = $1 * $3;}
    | term OP_DIV factor {$$ = $1 / $3;}
    | term OP_POW factor {$$ = pow($1,$3);}
    | factor {$$=$1;}
;
factor:
    NUM {$$=$1;}
    | LEFT_BRACKET expr RIGHT_BRACKET {$$=$2;}
    | OP_SUB NUM %prec NEG {$$=-$2;}
    | fun
fun:
    OP_FUN LEFT_BRACKET expr RIGHT_BRACKET {
        if (strcmp("sin", $1) == 0)
        {
            $$=sin($3* PI / 180);
        }else if (strcmp("cos", $1) == 0)
        {
            $$=cos($3* PI / 180);
        }
        else if (strcmp("tan", $1) == 0)
        {
            $$=tan($3* PI / 180);
        }else{
            yyerror($1);

        }
    } // end

;

%%

void yyerror(char * s){
    printf("yyerror : %s\n", s);
}

double get_result(){
    return result;
}

// int main(void ){
//     yy_scan_string("sin(30)\n");
//     yyparse();
//     printf("%f\n", result);
//     return 0;
// }

double parse_expr(char * expr){
    // 这个是供外部调用的。
    printf("expr: %s\n", expr);
    YY_BUFFER_STATE buf = yy_scan_string(expr);
    yyparse();
    double result = get_result();
    yy_delete_buffer(buf);
    return result;

}
```

 

main.c

```
// 这个是一个简单的调用，看看结果的
#include <stdio.h>

extern double parse_expr(char * expr);


int main()
{
    double  result = parse_expr((char *)"1+2");
    printf("%f\n", result);
}
```

makefile.bat

这个是运行在win下的，flex=lex, bison=yacc

```
del calc.tab.c
del calc.tab.h
del lex.yy.c
bison -d calc.y 
flex calc.l
gcc -o bc *.c -lm
```

makefile

```
yacc = bison
lex = flex

all:
  $(yacc) -d calc.y 
  $(lex) calc.l
  #  "要有-lm，表示添加math的库。-l表示添加库，math的库名称是libm.so，所以是-lm"
  gcc -o bc *.c -lm

clean:
  rm -rf *.c bc *.h
```
