---
layout: post
title: "EBNF翻译"
date: "2017-10-31"
categories: 
  - "bnf_ebnf"
---

# Extended Backus–Naur form

From Wikipedia, the free encyclopedia

In [computer science](https://en.wikipedia.org/wiki/Computer_science "Computer science"), **extended Backus-Naur form** (**EBNF**) is a family of [metasyntax](https://en.wikipedia.org/wiki/Metasyntax "Metasyntax") notations, any of which can be used to express a [context-free grammar](https://en.wikipedia.org/wiki/Context-free_grammar "Context-free grammar"). EBNF is used to make a formal description of a [formal language](https://en.wikipedia.org/wiki/Formal_language "Formal language") which can be a computer [programming language](https://en.wikipedia.org/wiki/Programming_language "Programming language"). They are extensions of the basic [Backus–Naur form](https://en.wikipedia.org/wiki/Backus%E2%80%93Naur_form "Backus–Naur form") (BNF) metasyntax notation.

The earliest EBNF was originally developed by [Niklaus Wirth](https://en.wikipedia.org/wiki/Niklaus_Wirth "Niklaus Wirth") incorporating some of the concepts (with a different syntax and notation) from [Wirth syntax notation](https://en.wikipedia.org/wiki/Wirth_syntax_notation "Wirth syntax notation"). However, many variants of EBNF are in use. The [International Organization for Standardization](https://en.wikipedia.org/wiki/International_Organization_for_Standardization "International Organization for Standardization") has adopted an EBNF standard ([ISO/IEC 14977](http://standards.iso.org/ittf/PubliclyAvailableStandards/s026153_ISO_IEC_14977_1996\(E\).zip)). This article uses EBNF as specified by the ISO for examples applying to all EBNFs. Other EBNF variants use somewhat different syntactic conventions.

## Contents

 \[hide\] 

- [1Basics](https://en.wikipedia.org/wiki/Extended_Backus%E2%80%93Naur_form#Basics)
    - [1.1Table of symbols](https://en.wikipedia.org/wiki/Extended_Backus%E2%80%93Naur_form#Table_of_symbols)
    - [1.2Examples](https://en.wikipedia.org/wiki/Extended_Backus%E2%80%93Naur_form#Examples)
- [2Advantages over BNF](https://en.wikipedia.org/wiki/Extended_Backus%E2%80%93Naur_form#Advantages_over_BNF)
- [3Conventions](https://en.wikipedia.org/wiki/Extended_Backus%E2%80%93Naur_form#Conventions)
- [4Extensibility](https://en.wikipedia.org/wiki/Extended_Backus%E2%80%93Naur_form#Extensibility)
- [5Related work](https://en.wikipedia.org/wiki/Extended_Backus%E2%80%93Naur_form#Related_work)
- [6See also](https://en.wikipedia.org/wiki/Extended_Backus%E2%80%93Naur_form#See_also)
- [7References](https://en.wikipedia.org/wiki/Extended_Backus%E2%80%93Naur_form#References)
- [8External links](https://en.wikipedia.org/wiki/Extended_Backus%E2%80%93Naur_form#External_links)

## Basics\[[edit](https://en.wikipedia.org/w/index.php?title=Extended_Backus%E2%80%93Naur_form&action=edit&section=1 "Edit section: Basics")\]

EBNF is a [code](https://en.wikipedia.org/wiki/Code "Code") that expresses the grammar of a formal language. An EBNF consists of [terminal symbols](https://en.wikipedia.org/wiki/Terminal_symbol "Terminal symbol") and non-terminal production rules which are the restrictions governing how terminal symbols can be combined into a legal sequence. Examples of terminal symbols include [alphanumeric characters](https://en.wikipedia.org/wiki/Alphanumerics "Alphanumerics"), [punctuation marks](https://en.wikipedia.org/wiki/Punctuation "Punctuation"), and [whitespace characters](https://en.wikipedia.org/wiki/Whitespace_character "Whitespace character").

EBNF由minal symbols（终端语符）和non-terminal production rules（非终端符生成规则，）组成。

 

The EBNF defines [production rules](https://en.wikipedia.org/wiki/Production_\(computer_science\) "Production (computer science)") where sequences of symbols are respectively assigned to a [nonterminal](https://en.wikipedia.org/wiki/Terminal_and_nonterminal_symbols "Terminal and nonterminal symbols"):

```
digit excluding zero = "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;
digit                = "0" | digit excluding zero ;

```

This production rule defines the nonterminal _digit_ which is on the left side of the assignment（非终端符定义在表达式的左边）. The vertical bar represents an alternative and the terminal symbols are enclosed with quotation marks followed by a semicolon as terminating character. Hence a _digit_ is a _0_ or a _digit excluding zero_ that can be _1_ or _2_ or _3_ and so forth until _9_.

A production rule can also include a sequence of terminals or nonterminals, each separated by a comma:

（生成规则还可以包括一系列终端或者非终端字符）

```
twelve                          = "1", "2" ;
two hundred one                 = "2", "0", "1" ;
three hundred twelve            = "3", twelve ;
twelve thousand two hundred one = twelve, two hundred one ;

```

Expressions that may be omitted or repeated can be represented through curly braces { ... }:

用花括号表示省略或者重复的。

```
natural number = digit excluding zero, { digit } ;

```

In this case, the strings _1_, _2_, ...,_10_,...,_12345_,... are correct expressions. To represent this, everything that is set within the curly braces may be repeated arbitrarily often, including not at all.

An option can be represented through squared brackets \[ ... \]. That is, everything that is set within the square brackets may be present just once, or not at all:

（方括号表示只能出现一次或者根本不存在）

```
integer = "0" | [ "-" ], natural number ;

```

Therefore an [integer](https://en.wikipedia.org/wiki/Integer "Integer") is a zero (_0_) or a [natural number](https://en.wikipedia.org/wiki/Natural_number "Natural number") that may be preceded by an optional [minus sign](https://en.wikipedia.org/wiki/Plus_and_minus_signs#Minus_sign "Plus and minus signs").

EBNF also provides, among other things, the syntax to describe repetitions (of a specified number of times), to exclude some part of a production, and to insert comments in an EBNF grammar.

### Table of symbols\[[edit](https://en.wikipedia.org/w/index.php?title=Extended_Backus%E2%80%93Naur_form&action=edit&section=2 "Edit section: Table of symbols")\]

The following represents a proposed ISO/IEC 14977 standard, by R. S. Scowen, page 7, table 1.

| Usage | Notation |
| --- | --- |
| definition（定义） | \= |
| [concatenation（连接）](https://en.wikipedia.org/wiki/Concatenation "Concatenation") | , |
| termination （终止） | ; |
| [alternation （2选一）](https://en.wikipedia.org/wiki/Alternation_\(formal_language_theory\) "Alternation (formal language theory)") | | |
| optional （选项） | \[ ... \] |
| repetition （重复） | { ... } |
| grouping （分组） | ( ... ) |
| terminal string  （终端语符列） | " ... " |
| terminal string | ' ... ' |
| comment （评论，应该是注释吧） | (\* ... \*) |
| special sequence （特殊序列） | ? ... ? |
| exception （例外） | \- |

### Examples\[[edit](https://en.wikipedia.org/w/index.php?title=Extended_Backus%E2%80%93Naur_form&action=edit&section=3 "Edit section: Examples")\]

Even EBNF can be described using EBNF. Consider the sketched grammar below:

```
letter = "A" | "B" | "C" | "D" | "E" | "F" | "G"
       | "H" | "I" | "J" | "K" | "L" | "M" | "N"
       | "O" | "P" | "Q" | "R" | "S" | "T" | "U"
       | "V" | "W" | "X" | "Y" | "Z" | "a" | "b"
       | "c" | "d" | "e" | "f" | "g" | "h" | "i"
       | "j" | "k" | "l" | "m" | "n" | "o" | "p"
       | "q" | "r" | "s" | "t" | "u" | "v" | "w"
       | "x" | "y" | "z" ;
digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;
symbol = "[" | "]" | "{" | "}" | "(" | ")" | "<" | ">"
       | "'" | '"' | "=" | "|" | "." | "," | ";" ;
character = letter | digit | symbol | "_" ;
 
identifier = letter , { letter | digit | "_" } ;
terminal = "'" , character , { character } , "'" 
         | '"' , character , { character } , '"' ;
 
lhs = identifier ;
rhs = identifier
     | terminal
     | "[" , rhs , "]"
     | "{" , rhs , "}"
     | "(" , rhs , ")"
     | rhs , "|" , rhs
     | rhs , "," , rhs ;

rule = lhs , "=" , rhs , ";" ;
grammar = { rule } ;

```

A [Pascal](https://en.wikipedia.org/wiki/Pascal_\(programming_language\) "Pascal (programming language)")\-like programming language that allows only assignments can be defined in EBNF as follows:

```
 (* a simple program syntax in EBNF − Wikipedia *)
 program = 'PROGRAM', white space, identifier, white space, 
            'BEGIN', white space, 
            { assignment, ";", white space }, 
            'END.' ;
 identifier = alphabetic character, { alphabetic character | digit } ;
 number = [ "-" ], digit, { digit } ;
 string = '"' , { all characters - '"' }, '"' ;
 assignment = identifier , ":=" , ( number | identifier | string ) ;
 alphabetic character = "A" | "B" | "C" | "D" | "E" | "F" | "G"
                      | "H" | "I" | "J" | "K" | "L" | "M" | "N"
                      | "O" | "P" | "Q" | "R" | "S" | "T" | "U"
                      | "V" | "W" | "X" | "Y" | "Z" ;
 digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;
 white space = ? white space characters ? ;
 all characters = ? all visible characters ? ;

```

A syntactically correct program then would be:

```
 PROGRAM DEMO1
 BEGIN
   A:=3;
   B:=45;
   H:=-100023;
   C:=A;
   D123:=B34A;
   BABOON:=GIRAFFE;
   TEXT:="Hello world!";
 END.

```

The language can easily be extended with [control flows](https://en.wikipedia.org/wiki/Control_flow "Control flow"), arithmetical expressions, and Input/Output instructions. Then a small, usable programming language would be developed.

## Advantages over BNF\[[edit](https://en.wikipedia.org/w/index.php?title=Extended_Backus%E2%80%93Naur_form&action=edit&section=4 "Edit section: Advantages over BNF")\]

Any [grammar](https://en.wikipedia.org/wiki/Grammar "Grammar") defined in EBNF can also be represented in BNF, though representations in the latter are generally lengthier. E.g., options and repetitions cannot be directly expressed in BNF and require the use of an intermediate rule or alternative production defined to be either nothing or the optional production for option, or either the repeated production of itself, recursively, for repetition. The same constructs can still be used in EBNF.

The BNF uses the symbols (<, >, |, ::=) for itself, but does not include quotes around terminal strings. This prevents these characters from being used in the languages, and requires a special symbol for the empty string. In EBNF, [terminals](https://en.wikipedia.org/wiki/Terminal_symbol "Terminal symbol") are strictly enclosed within quotation marks ("..." or '...'). The angle brackets ("<...>") for [nonterminals](https://en.wikipedia.org/wiki/Terminal_and_nonterminal_symbols "Terminal and nonterminal symbols") can be omitted.

BNF syntax can only represent a rule in one line, whereas in EBNF a terminating character, the semicolon, marks the end of a rule.

Furthermore, EBNF includes mechanisms for enhancements, defining the number of repetitions, excluding alternatives, comments, etc.

## Conventions\[[edit](https://en.wikipedia.org/w/index.php?title=Extended_Backus%E2%80%93Naur_form&action=edit&section=5 "Edit section: Conventions")\]

1\. The following conventions are used:

- Each meta-identifier of Extended BNF is written as one or more words joined together by [hyphens](https://en.wikipedia.org/wiki/Hyphen "Hyphen").
- A meta-identifier ending with _\-symbol_ is the name of a terminal symbol of Extended BNF.

2\. The normal character representing each operator of Extended BNF and its implied precedence is (highest precedence at the top):

```
* repetition-symbol
- except-symbol
, concatenate-symbol
| definition-separator-symbol
= defining-symbol
; terminator-symbol
. terminator-symbol

```

3\. The normal precedence is overridden by the following bracket pairs:

```
'  first-quote-symbol            first-quote-symbol  '
"  second-quote-symbol          second-quote-symbol  "
(* start-comment-symbol          end-comment-symbol *)
(  start-group-symbol              end-group-symbol  )
[  start-option-symbol            end-option-symbol  ]
{  start-repeat-symbol            end-repeat-symbol  }
?  special-sequence-symbol  special-sequence-symbol  ?

```

The first-quote-symbol is the [apostrophe](https://en.wikipedia.org/wiki/Apostrophe "Apostrophe") as defined by ISO/IEC 646:1991, that is to say Unicode U+0027 ('); the font used in ISO/IEC 14977:1996(E) renders it very much like the acute, Unicode U+00B4 (´), so confusion sometimes arises. However, the ISO Extended BNF standard invokes ISO/IEC 646:1991, "ISO 7-bit coded character set for information interchange", as a normative reference and makes no mention of any other character sets, so formally, there is no confusion with Unicode characters outside the 7-bit ASCII range.

As examples, the following syntax rules illustrate the facilities for expressing repetition:

```
aa = "A";
bb = 3 * aa, "B";
cc = 3 * [aa], "C";
dd = {aa}, "D";
ee = aa, {aa}, "E";
ff = 3 * aa, 3 * [aa], "F";
gg = {3 * aa}, "G";

```

Terminal strings defined by these rules are as follows:

```
aa: A
bb: AAAB
cc: C AC AAC AAAC
dd: D AD AAD AAAD AAAAD etc.
ee: AE AAE AAAE AAAAE AAAAAE etc.
ff: AAAF AAAAF AAAAAF AAAAAAF
gg: G AAAG AAAAAAG etc.

```

## Extensibility\[[edit](https://en.wikipedia.org/w/index.php?title=Extended_Backus%E2%80%93Naur_form&action=edit&section=6 "Edit section: Extensibility")\]

According to the ISO 14977 standard EBNF is meant to be extensible, and two facilities are mentioned. The first is part of EBNF grammar, the special sequence, which is arbitrary text enclosed with question marks. The interpretation of the text inside a special sequence is beyond the scope of the EBNF standard. For example, the space character could be defined by the following rule:

```
 space = ? US-ASCII character 32 ?;

```

The second facility for extension is using the fact that parentheses cannot in EBNF be placed next to identifiers (they must be concatenated with them). The following is valid EBNF:

```
 something = foo, ( bar );

```

The following is _not_ valid EBNF:

```
 something = foo ( bar );

```

Therefore, an extension of EBNF could use that notation. For example, in a Lisp grammar, function application could be defined by the following rule:

```
 function application = list( symbol, { expression } );
```
