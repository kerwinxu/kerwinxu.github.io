---
layout: post
title: "javacc教程"
date: "2022-02-23"
categories: 
  - "java"
---

# 例子

文件 parser.jj

```
/**
 * JavaCC template file created by SF JavaCC plugin 1.5.28+ wizard for JavaCC 1.5.0+

 * <expression> ::= <multiplicative_expression> {"+"  <multiplicative_expression> | "-" <multiplicative_expression>}
 * <multiplicative_expression> ::= <primary_expression> {"*" <primary_expression> | "/"<primary_expression> | "%"<primary_expression>}
 * <primary_expression> ::= <number> | "(" <expression> ")"
 */
options
{
  static = false;  // 所有成员或者方法是否被定义为静态。
}


// 解析器类的定义。
PARSER_BEGIN(parser)  
package io.github.kerwinxu.parser;

import java.io.*; // 需要导入流

public class parser
{
  public static void main(String args []) throws ParseException
  {
    // 遍历命令行
    for(String arg : args)
    {
      System.out.println((new parser(new StringReader(arg))).expr());
    }
  }
}

PARSER_END(parser)

// 跳过的
SKIP :
{
  " "
| "\r"
| "\t"
| "\n"
}

// 词法描述如下

TOKEN : /* OPERATORS */
{
  < PLUS : "+" >
| < MINUS : "-" >
| < MULTIPLY : "*" >
| < DIVIDE : "/" >
| < LPAREN : "(" >
| < RPAREN : ")" >
}

TOKEN :
{
  < FLOAT : (< DIGIT >)+ ("." (< DIGIT >)+ )*  >
| < #DIGIT : [ "0"-"9" ] >
}

// 语法分析器如下

// 一级的，只是加减。
double expr():
{
  double left;
  double right;
}
{
  left = multi_expr()
  (
    < PLUS > right=multi_expr() { left += right ;}  // 这里表示遇到+，就执行花括号里边的操作。
  | <MINUS > right=multi_expr() { left -= right ;}
  )*  // 这个*表示一个或者多个
  {
    return left;
  }
}

// 二级的，乘除
double multi_expr():
{
  double left;
  double right;
}
{
  left = primary_expr()
  (
    < MULTIPLY > right=primary_expr() { left *= right ;}
  | <DIVIDE >    right=primary_expr() { left /= right ;}
  )*
  {
    return left;
  }
}

// 三级的
double primary_expr():
{
  Token t=null;
  double num;
}
{
  t=< FLOAT > {  return Double.parseDouble(t.image); }                // 遇到的是数字
  | < LPAREN > num=expr() < RPAREN > { return num ; }                 // 括号
  |< MINUS > t=< FLOAT > {  return 0- Double.parseDouble(t.image); }  // 负数

}


```

 

# 引用

- [JavaCC官方入门指南-概述](https://www.e-learn.cn/topic/3606311)
