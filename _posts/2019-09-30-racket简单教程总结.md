---
layout: post
title: "racket简单教程总结"
date: "2019-09-30"
categories: 
  - "lisp"
---

# 数据类型

## 简单数据类型

### 数字

```
;;; 数字
9999999999999999999999 ; 整数
#b111                  ; 二进制数字 => 7
#o111                  ; 八进制数字 => 73
#x111                  ; 十六进制数字 => 273
3.14                   ; 实数
6.02e+23
1/2                    ; 有理数
```

### 布尔型

```
;;; 布尔类型
#t ; 为真
#f ; 为假，#f 之外的任何值都是真
(not #t) ; => #f
(and 0 #f (error "doesn't get here")) ; => #f
(or #f 0 (error "doesn't get here"))  ; => 0
```

### 字符

```
;;; 字符
#\A ; => #\A
#\λ ; => #\λ
#\u03BB ; => #\λ
```

### 字符串

字符组成的定长数组而已

```
;;; 字符串是字符组成的定长数组
"Hello, world!"
"Benjamin \"Bugsy\" Siegel"   ; \是转义字符
"Foo\tbar\41\x21\u0021\a\r\n" ; 包含C语言的转义字符，和Unicode
"λx:(μα.α→α).xx"              ; 字符串可以包含Unicode字符
```

#### 字符串操作

(require racket/string)

[![no img]](http://127.0.0.1/?attachment_id=2735)

[![no img]](http://127.0.0.1/?attachment_id=2736)

 

## 复合数据类型

### 点对

这个或许是最重要的一个数据类型了吧

```
;;; 点对 (不可变的)
;; `cons' 返回点对, `car' 和 `cdr' 从对中提取第1个
;; 和第2个元素
(cons 1 2) ; => '(1 . 2)
(car (cons 1 2)) ; => 1
(cdr (cons 1 2)) ; => 2
```

这个点对的第二项可以同样是点对，由此组成一个列表或者一个树。

### 结构

```
;; 结构体
(struct dog (name breed age))
(define my-pet
  (dog "lassie" "collie" 5))
my-pet ; => #<dog>
(dog? my-pet) ; => #t
```

### 列表

```
;; 列表由链表构成, 由 `cons' 的结果
;; 和一个 `null' (或者 '()) 构成，后者标记了这个列表的结束
(cons 1 (cons 2 (cons 3 null))) ; => '(1 2 3)
;; `list' 给列表提供了一个非常方便的可变参数的生成器
(list 1 2 3) ; => '(1 2 3)
;; 一个单引号也可以用来表示一个列表字面量
'(1 2 3) ; => '(1 2 3)
```

#### 列表操作

##### 添加

```
;; 仍然可以使用 `cons' 在列表的开始处添加一项
(cons 4 '(1 2 3)) ; => '(4 1 2 3)
```

##### 合并

```
;; `append' 函数可以将两个列表合并
(append '(1 2) '(3 4)) ; => '(1 2 3 4)
```

### 向量

向量是定长的数组

```
;; 向量是定长的数组
#(1 2 3) ; => '#(1 2 3)
```

#### 合并向量

```
;; 使用 `vector-append' 方法将2个向量合并
(vector-append #(1 2 3) #(4 5 6)) ; => #(1 2 3 4 5 6)
```

### 集合

不重复

```
;; 从一个列表创建一个Set
(list->set '(1 2 3 1 2 3 3 2 1 3 2 1)) ; => (set 1 2 3)

```

 

#### 集合操作

##### 增加

```
;; 使用 `set-add' 增加一个成员
;; (函数式特性: 这里会返回一个扩展后的Set，而不是修改输入的值)
(set-add (set 1 2 3) 4) ; => (set 1 2 3 4)
```

##### 删除

```
;; 使用 `set-remove' 移除一个成员
(set-remove (set 1 2 3) 1) ; => (set 2 3)
```

##### 是否存在

```
;; 使用 `set-member?' 测试成员是否存在
(set-member? (set 1 2 3) 1) ; => #t
(set-member? (set 1 2 3) 4) ; => #f
```

### 不可变 散列表 hash

```
;; 创建一个不变的散列表  
(define m (hash 'a 1 'b 2 'c 3))
```

#### 取值

```
;; 根据键取得值
(hash-ref m 'a) ; => 1
;; 获取一个不存在的键是一个异常
; (hash-ref m 'd) => 没有找到元素
;; 你可以给不存在的键提供一个默认值
(hash-ref m 'd 0) ; => 0
```

#### 扩展

```
;; 使用 `hash-set' 来扩展一个不可变的散列表
;; (返回的是扩展后的散列表而不是修改它)
(define m2 (hash-set m 'd 4))
m2 ; => '#hash((b . 2) (a . 1) (d . 4) (c . 3))
;; 记住，使用 `hash` 创建的散列表是不可变的
m ; => '#hash((b . 2) (a . 1) (c . 3))  <-- no `d'
```

#### 移除

```
;; 使用 `hash-remove' 移除一个键值对 (函数式特性，m并不变)
(hash-remove m 'a) ; => '#hash((b . 2) (c . 3))
```

### 可变散列表

```
;; 创建一个空的可变散列表，然后操作它
(define m3 (make-hash))
(hash-set! m3 'a 1)
(hash-set! m3 'b 2)
(hash-set! m3 'c 3)
(hash-ref m3 'a)   ; => 1
(hash-ref m3 'd 0) ; => 0
(hash-remove! m3 'a)
```

# 表达式和定义

## 函数调用

(proc-expr arg-expr ...) 是一个函数调用，也被称为一个应用程序（procedure application）——当proc-expr不是一个被绑定为一个语法翻译器（如if或define）的标识符时。

### 求值顺序和实参数量

一个函数调用通过首先求值proc-expr并都按顺序（由左至右）来求值。然后，如果arg-expr产生一个接受arg-expr提供的所有参数的函数，这个函数被调用。否则，将引发一个异常。

### 关键词参数

除了通过位置参数外，有些函数接受关键字参数（keyword arguments）。因此，一个arg可以是一个arg-keyword arg-expr序列而不仅仅只是一个arg-expr：

> > <table class="boxed RBoxed" cellspacing="0" cellpadding="0"><tbody><tr><td><span class="RktPn">(</span><span class="RktVar">proc-expr</span><span class="hspace">&nbsp;</span><span class="RktVar">arg</span><span class="hspace">&nbsp;</span><span class="RktMeta">...</span><span class="RktPn">)</span></td></tr><tr><td><span class="stt">&nbsp;</span></td></tr><tr><td><div class="table-box"><table class="specgrammar" cellspacing="0" cellpadding="0"><tbody><tr><td align="right" valign="baseline"><span class="RktVar">arg</span></td><td align="left" valign="baseline"><span class="stt">&nbsp;</span></td><td align="center" valign="baseline">=</td><td align="left" valign="baseline"><span class="stt">&nbsp;</span></td><td align="left" valign="baseline"><span class="RktVar">arg-expr</span></td></tr><tr><td align="right" valign="baseline"><span class="stt">&nbsp;</span></td><td align="left" valign="baseline"><span class="stt">&nbsp;</span></td><td align="center" valign="baseline">|</td><td align="left" valign="baseline"><span class="stt">&nbsp;</span></td><td align="left" valign="baseline"><span class="RktVar">arg-keyword</span><span class="hspace">&nbsp;</span><span class="RktVar">arg-expr</span></td></tr></tbody></table></div></td></tr></tbody></table>
> > 
> >  

例如：

用"super.rkt"作为一个位置参数调用这个函数绑定到go，并用'fast作为一个参数与#:mode关键字关联。一个关键字隐式地与它后面的表达式配对。

既然一个关键字本身不是一个表达式，那么

> (go "super.rkt" #:mode #:fast)

就是一个语法错误。#:mode关键字必须跟着一个表达式以产生一个参数值，并且#:fast不是一个表达式。

关键字arg的顺序决定arg-expr求值的顺序，而一个函数接受关键字参数不依赖于参数列表中的位置。上面对go的调用可以等价地编写为：

> (go#:mode'fast"super.rkt")

### apply函数

它使用一个函数和一个list参数，并将函数应用到列表中的值

```
(define (avg lst)
  (/ (apply + lst) (length lst)))
```

## lambda函数（过程）

个lambda表达式创建一个函数。在最简单的情况，一个lambda表达式具有的表：

一个具有n个arg-id的lambda表接受n个参数

```
(lambda (arg-id ...)
  body ...+)
```

### 申明一个剩余（rest）参数

一个lambda表达式也可以有这种表：

```
(lambda rest-id
  body ...+)
```

也就是说，一个lambda表达式可以有一个没有被圆括号包围的单个rest-id。所得到的函数接受任意数量的参数，并且这个参数放入一个绑定到rest-id的列表：

lambda表还支持必需参数与一个rest-id组合：

```
(lambda (arg-id ...+ . rest-id)
  body ...+)
```

这个表的结果是一个函数，它至少需要与arg-id一样多的参数，并且还接受任意数量的附加参数。

```
(define max-mag
  (lambda (num . nums)
    (apply max (map magnitude (cons num nums)))))
```

### 声明可选（optional）参数

```
(lambda gen-formals
  body ...+)
 
gen-formals	=	 	(arg ...)
 	 	|	 	rest-id
 	 	|	 	(arg ...+ . rest-id)
 	 	 	 	 
arg	 	=	 	arg-id
 	 	|	 	[arg-id default-expr]
```

表的一个参数\[arg-id default-expr\]是可选的。当这个参数不在一个应用程序中提供，default-expr产生默认值。default-expr可以引用任何前面的arg-id，并且下面的每个arg-id也必须应该有一个默认值。

Examples:

```
(define greet
  (lambda (given [surname "Smith"])
    (string-append "Hello, " given " " surname)))
```

```
(define greet
  (lambda (given [surname (if (equal? given "John")
                              "Doe"
                              "Smith")])
    (string-append "Hello, " given " " surname)))
```

### 声明关键字（keyword）参数

一个lambda表可以声明一个参数来通过关键字传递，而不是通过位置传递。关键字参数可以与位置参数混合，而且默认值表达式可以提供给两种参数：

```
(lambda gen-formals
  body ...+)
 
gen-formals	=	 	(arg ...)
 	 	|	 	rest-id
 	 	|	 	(arg ...+ . rest-id)
 	 	 	 	 
arg	 	=	 	arg-id
 	 	|	 	[arg-id default-expr]
 	 	|	 	arg-keyword arg-id
 	 	|	 	arg-keyword [arg-id default-expr]
```

由一个应用程序使用同一个arg-keyword提供一个参数指定为arg-keywordarg-id。关键字的位置——在参数列表中的标识符配对与一个应用程序中的参数匹配并不重要，因为它将通过关键字而不是位置与一个参数值匹配。

```
(define greet
  (lambda (given #:last surname)
    (string-append "Hello, " given " " surname)))
```

一个arg-keyword\[arg-iddefault-expr\]参数指定一个带一个默认值的关键字参数。

```
(define greet
  (lambda (#:hi [hi "Hello"] given #:last [surname "Smith"])
    (string-append hi ", " given " " surname)))
```

## 实参数量感知函数：case-lambda

case-lambda表创建一个函数，它可以根据提供的参数数量而具有完全不同的行为。一个case-lambda表达式有这样的表：

```
(case-lambda
  [formals body ...+]
  ...)
 
formals	 	=	 	(arg-id ...)
 	 	|	 	rest-id
 	 	|	 	(arg-id ...+ . rest-id)
```

每个\[formalsbody...+\]类似于(lambdaformalsbody...+)。应用以case-lambda生成一个函数类似于应用一个lambda给匹配给定参数数量的第一种情况。

```
(define greet
  (case-lambda
    [(name) (string-append "Hello, " name)]
    [(given surname) (string-append "Hello, " given " " surname)]))
```

一个case-lambda函数不能直接支持可选参数或关键字参数。

## 定义

基本的定义

```
(define id expr)
```

### 函数简写

```
(define (id arg ...) body ...+)

```

这是以下内容的简写：

> (defineid(lambda(arg...)body...+))
> 
>  
> 
> 函数简写通过define也支持一个剩余参数（rest argument）（即，一个最终参数以在一个列表中收集额外参数）：

```
(define (id arg ... . rest-id) body ...+)
```

它是以下内容的一个简写：

> (defineid(lambda(arg.... rest-id)body...+))

 

### 柯里函数简写

注意下面的make-add-suffix函数，它接收一个字符串并返回另一个接受一个字符串的函数：

```
(define make-add-suffix
  (lambda (s2)
    (lambda (s) (string-append s s2))))
```

虽然不常见，但make-add-suffix的结果可以直接调用，就像这样：

某种意义上说，make-add-suffix是一个函数，接受两个参数，但一次只接受一个参数。一个函数，它接受它的参数的一些并返回一个函数以接受更多，这种函数有时被称为一个柯里函数（curried function）。

## 局部绑定

虽然内部define可用于局部绑定，Racket提供了三种表，它们给予程序员在绑定方面的更多控制：let、let\*和letrec。

```
(let ([id expr] ...) body ...+)

```

let\*的语法和let的一样：

不同的是，每个id可在以后的expr使用中以及body中找到。此外，id不需要有区别，并且最近的绑定是可见的一个。

# 控制部分

## 判断

### 判断相等

```
;; 判断数字使用 `='
(= 3 3.0) ; => #t
(= 2 1) ; => #f
;; 判断对象使用 `eq?'
(eq? 3 3) ; => #t
(eq? 3 3.0) ; => #f
(eq? (list 3) (list 3)) ; => #f
;; 判断集合使用 `equal?'
(equal? (list 'a 'b) (list 'a 'b)) ; => #t
(equal? (list 'a 'b) (list 'b 'a)) ; => #f
```

## 条件判断

```
;;; 条件判断
(if #t               ; 测试表达式
    "this is true"   ; 为真的表达式
    "this is false") ; 为假的表达式
; => "this is true"
;; 注意, 除 `#f` 之外的所有值都认为是真
(member 'Groucho '(Harpo Groucho Zeppo)) ; => '(Groucho Zeppo)
(if (member 'Groucho '(Harpo Groucho Zeppo))
    'yep
    'nope)
; => 'yep
```

## coud 判断

```
;; `cond' 会进行一系列的判断来选择一个结果
(cond [(> 2 2) (error "wrong!")]
      [(< 2 2) (error "wrong again!")]
      [else 'ok]) ; => 'ok
```

对于cond从句的方括号的使用是一种惯例。在Racket中，圆括号和方括号实际上是可互换的，只要(匹配)或\[匹配\]即可。在一些关键的地方使用方括号使Racket代码更易读。

## 模式匹配

```
;;; 模式匹配
(define (fizzbuzz? n)
  (match (list (remainder n 3) (remainder n 5))
    [(list 0 0) 'fizzbuzz]
    [(list 0 _) 'fizz]
    [(list _ 0) 'buzz]
    [_          #f]))
(fizzbuzz? 15) ; => 'fizzbuzz
(fizzbuzz? 37) ; => #f
```

## 循环

```
;;; 循环
;; 循环可以使用递归(尾递归)
(define (loop i)
  (when (< i 10)
    (printf "i=~a\n" i)
    (loop (add1 i))))
(loop 5) ; => i=5, i=6, ...
;; 类似的，可以使用 `let` 定义
(let loop ((i 0))
  (when (< i 10)
    (printf "i=~a\n" i)
    (loop (add1 i)))) ; => i=0, i=1, ...
;; 看上面的例子怎么增加一个新的 `loop' 形式, 但是 Racket 已经有了一个非常
;; 灵活的 `for' 了:
(for ([i 10])
  (printf "i=~a\n" i)) ; => i=0, i=1, ...
(for ([i (in-range 5 10)])
  (printf "i=~a\n" i)) ; => i=5, i=6, ...
;;; 其他形式的迭代
;; `for' 允许在很多数据结构中迭代:
;; 列表, 向量, 字符串, Set, 散列表, 等...
(for ([i (in-list '(l i s t))])
  (displayln i))
(for ([i (in-vector #(v e c t o r))])
  (displayln i))
(for ([i (in-string "string")])
  (displayln i))
(for ([i (in-set (set 'x 'y 'z))])
  (displayln i))
(for ([(k v) (in-hash (hash 'a 1 'b 2 'c 3 ))])
  (printf "key:~a value:~a\n" k v))
;;; 更多复杂的迭代
;; 并行扫描多个序列 (遇到长度小的就停止)
(for ([i 10] [j '(x y z)]) (printf "~a:~a\n" i j))
; => 0:x 1:y 2:z
;; 嵌套循环
(for* ([i 2] [j '(x y z)]) (printf "~a:~a\n" i j))
; => 0:x, 0:y, 0:z, 1:x, 1:y, 1:z
;; 带有条件判断的 `for`
(for ([i 1000]
      #:when (> i 5)
      #:unless (odd? i)
      #:break (> i 10))
  (printf "i=~a\n" i))
; => i=6, i=8, i=10
;;; 更多的例子帮助你加深理解..
;; 和 `for' 循环非常像 -- 收集结果
(for/list ([i '(1 2 3)])
  (add1 i)) ; => '(2 3 4)
(for/list ([i '(1 2 3)] #:when (even? i))
  i) ; => '(2)
(for/list ([i 10] [j '(x y z)])
  (list i j)) ; => '((0 x) (1 y) (2 z))
(for/list ([i 1000] #:when (> i 5) #:unless (odd? i) #:break (> i 10))
  i) ; => '(6 8 10)
(for/hash ([i '(1 2 3)])
  (values i (number->string i)))
; => '#hash((1 . "1") (2 . "2") (3 . "3"))
;; 也有很多其他的内置方法来收集循环中的值:
(for/sum ([i 10]) (* i i)) ; => 285
(for/product ([i (in-range 1 11)]) (* i i)) ; => 13168189440000
(for/and ([i 10] [j (in-range 10 20)]) (< i j)) ; => #t
(for/or ([i 10] [j (in-range 0 20 2)]) (= i j)) ; => #t
;; 如果需要合并计算结果, 使用 `for/fold'
(for/fold ([sum 0]) ([i '(1 2 3 4)]) (+ sum i)) ; => 10
;; (这个函数可以在大部分情况下替代普通的命令式循环)
```

 

# 异常

```
;; 要捕获一个异常，使用 `with-handlers' 形式
(with-handlers ([exn:fail? (lambda (exn) 999)])
  (+ 1 "2")) ; => 999
(with-handlers ([exn:break? (lambda (exn) "no time")])
  (sleep 3)
  "phew") ; => "phew", 如果你打断了它，那么结果 => "no time"
;; 使用 `raise' 抛出一个异常后者其他任何值
(with-handlers ([number?    ; 捕获抛出的数字类型的值
                 identity]) ; 将它们作为普通值
  (+ 1 (raise 2))) ; => 2

```

 

# 模块

```
;; 模块让你将你的代码组织为多个文件，成为可重用的模块，
;; 在这里，我们使用嵌套在本文的整个大模块
;; 里的子模块(从 "#lang" 这一行开始)
(module cake racket/base ; 基于 racket/base 定义一个 `cake` 模块
  (provide print-cake) ; 这个模块导出的函数
  (define (print-cake n)
    (show "   ~a   " n #\.)
    (show " .-~a-. " n #\|)
    (show " | ~a | " n #\space)
    (show "---~a---" n #\-))
  (define (show fmt n ch) ; 内部函数
    (printf fmt (make-string n ch))
    (newline)))
;; 使用 `require` 从模块中得到所有 `provide` 的函数
(require 'cake) ; 这里的 `'`表示是本地的子模块
(print-cake 3)
; (show "~a" 1 #\A) ; => 报错, `show' 没有被导出，不存在
```

## 契约

```
;; 契约限制变量从模块中导入
(module bank-account racket
  (provide (contract-out
            [deposit (-> positive? any)] ; 数量一直是正值
            [balance (-> positive?)]))
  (define amount 0)
  (define (deposit a) (set! amount (+ amount a)))
  (define (balance) amount)
  )
(require 'bank-account)
(deposit 5)
(balance) ; => 5
;; 客户端尝试存储一个负值时会出错
;; (deposit -5) ; => deposit: contract violation
;; expected: positive?
;; given: -5
;; more details...
```

 

# 类和对象

```
;; 创建一个 fish% 类(%是给类绑定用的)
(define fish%
  (class object%
    (init size) ; 初始化的参数
    (super-new) ; 父类的初始化
    ;; 域
    (define current-size size)
    ;; 公共方法
    (define/public (get-size)
      current-size)
    (define/public (grow amt)
      (set! current-size (+ amt current-size)))
    (define/public (eat other-fish)
      (grow (send other-fish get-size)))))
;; 创建一个 fish% 类的示例
(define charlie
  (new fish% [size 10]))
;; 使用 `send' 调用一个对象的方法
(send charlie get-size) ; => 10
(send charlie grow 6)
(send charlie get-size) ; => 16
;; `fish%' 是一个普通的值，我们可以用它来混入
(define (add-color c%)
  (class c%
    (init color)
    (super-new)
    (define my-color color)
    (define/public (get-color) my-color)))
(define colored-fish% (add-color fish%))
(define charlie2 (new colored-fish% [size 10] [color 'red]))
(send charlie2 get-color)
;; 或者，不带名字
(send (new (add-color fish%) [size 10] [color 'red]) get-color)
```

# 宏

```
;; 宏让你扩展这门语言的语法
;; 让我们定义一个while循环
(define-syntax-rule (while condition body ...)
  (let loop ()
    (when condition
      body ...
      (loop))))
(let ([i 0])
  (while (< i  10)
    (displayln i)
    (set! i (add1 i))))
;; 宏是安全的，你不能修改现有的变量
(define-syntax-rule (swap! x y) ; !表示会修改
  (let ([tmp x])
    (set! x y)
    (set! y tmp)))
(define tmp 2)
(define other 3)
(swap! tmp other)
(printf "tmp = ~a; other = ~a\n" tmp other)
;; 变量 `tmp` 被重命名为 `tmp_1`
;; 避免名字冲突
;; (let ([tmp_1 tmp])
;;   (set! tmp other)
;;   (set! other tmp_1))
;; 但它们仍然会导致错误代码，比如:
(define-syntax-rule (bad-while condition body ...)
  (when condition
    body ...
    (bad-while condition body ...)))
;; 这个宏会挂掉，它产生了一个无限循环，如果你试图去使用它，编译器会进入死循环

```
