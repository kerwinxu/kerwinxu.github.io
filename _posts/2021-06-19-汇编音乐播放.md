---
layout: post
title: "汇编音乐播放"
date: "2021-06-19"
categories: ["计算机语言", "asm"]
---

版本1，

```asm
;定义数据段
data segment
     infor1 db 0Dh, 0AH, "welocom you to come here listeng! $"
 
     mus_freg  dw 330,294,262,294,3 dup (330)     ;频率表
               dw 3 dup (294),330,392,392
               dw 330,294,262,294,4 dup (330)
               dw 294,294,330,294,262,-1
     mus_time  dw 6 dup (25),50                   ;节拍表
               dw 2 dup (25,25,50)
               dw 12 dup (25),100
data ends
 
;栈段定义
stack segment stack
      db 200 dup(?)
stack ends
 
;--------字符串输出宏----------
SHOWBM MACRO b
     LEA DX,b
     MOV AH,9
     INT 21H
 ENDM
 
;----------音乐地址宏-----------
ADDRESS MACRO A,B
     LEA SI,A
     LEA BP,DS:B
ENDM
;-------------------------------
 
;代码段定义
code segment
     assume ds:data, ss:stack, cs:code
start:
     mov ax, data
     mov ds, ax
     mov ax, stack
     mov ss, ax
     mov sp, 200
    
     address mus_freg, mus_time
     call music
 
exit:     
     mov ah, 4cH
     int 21h
 
;------------发声-------------
gensound proc near
     push ax
     push bx
     push cx
     push dx
     push di
 
     mov al, 0b6H
     out 43h, al
     mov dx, 12h
     mov ax, 348ch
     div di
     out 42h, al
 
     mov al, ah
     out 42h, al
 
     in al, 61h
     mov ah, al
     or al, 3
     out 61h, al
wait1:
     mov cx, 3314
     call waitf
delay1:
     dec bx
     jnz wait1
 
     mov al, ah
     out 61h, al
 
     pop di
     pop dx
     pop cx
     pop bx
     pop ax
     ret 
gensound endp
 
;--------------------------
waitf proc near
      push ax
waitf1:
      in al,61h
      and al,10h
      cmp al,ah
      je waitf1
      mov ah,al
      loop waitf1
      pop ax
      ret
waitf endp
;--------------发声调用函数----------------
music proc near
      xor ax, ax
freg:
      mov di, [si]
      cmp di, 0FFFFH
      je end_mus
      mov bx, ds:[bp]
      call gensound
      add si, 2
      add bp, 2
      jmp freg
end_mus:
      ret
music endp
 
code ends
     end start
```

版本2

```
;定义数据段
data segment
    ; 如下的跟音乐相关的。
    mus_freg  dw 330,294,262,294,3 dup (330)     ;频率表
            dw 3 dup (294),330,392,392
            dw 330,294,262,294,4 dup (330)
            dw 294,294,330,294,262
    mus_time  dw 6 dup (25),50                   ;节拍表
            dw 2 dup (25,25,50)
            dw 12 dup (25),100
    music_count EQU 26   ; 如上有26个，每次播放只是播放一个。
    music_index dw 0     ; 这个是序号的意思。对应地址应该先乘以2.
data ends

;栈段定义
stack segment stack
    db 200 dup(0)
stack ends

;--------字符串输出宏----------
SHOWBM MACRO b
    LEA DX,b
    MOV AH,9
    INT 21H
ENDM




;代码段定义
code segment
    assume ds:data, ss:stack, cs:code
start:
    mov ax, data
    mov ds, ax
    mov ax, stack
    mov ss, ax
    mov sp, 200
    ;


exit:     
    ; 关闭定时器，否则到最后会一直以一个频率发声。
    AND AL, 0FCH 
    OUT 61H, AL
    mov ah, 4cH
    int 21h


;------------发声-------------
gensound proc near
    ; 先保存这个函数会有修改的寄存器。
    push ax
    push bx
    push cx
    push dx
    push di

    mov al, 0b6H ; 初始化定时器
    out 43h, al  ; 43h是8253定时器芯片的控制寄存器地址。
    ; 计算频率
    mov dx, 12h
    mov ax, 348ch
    div di ; 用除法计算。
    ; 先输出低位，
    out 42h, al
    ; 后输出高位。
    mov al, ah
    out 42h, al
    ; 打开扬声器
    in al, 61h
    mov ah, al
    or al, 3
    out 61h, al
    ; 如下是节拍
wait1:
    mov cx, 3314
    call waitf
delay1:
    dec bx
    jnz wait1
    mov al, ah
    out 61h, al
    ; 弹出修改的堆栈。
    pop di
    pop dx
    pop cx
    pop bx
    pop ax
    ret 
gensound endp

;--------------------------
waitf proc near
    push ax
waitf1:
    in al,61h
    and al,10h
    cmp al,ah
    je waitf1
    mov ah,al
    loop waitf1
    pop ax
    ret
waitf endp
;--------------发声调用函数----------------
music proc near
    ; 先保存寄存器
    push ax 
    push bx
    push cx
    push dx
    push si
    push di
    ; 计算偏移
    mov ax , music_index ; 取得现在的序号
    mov dx , 2           ; 乘以2
    mul dx               ; 结果放在ax，dx中。
    ; 这里要先判断一下是否结束了
    cmp ax , music_count
    jb music1
        ; 重新开始新的音乐
        mov ax , 0
        mov music_index , ax 
    music1:
        ; 然后根据这个下标计算地址
        lea si , mus_freg
        add si , ax
        mov di , [si]
        lea si , mus_time
        add si , ax
        mov bx , [si]
        call gensound
    
    ; 这里下一个
    mov ax , music_index
    inc ax 
    mov music_index, ax
    ; 恢复
    pop di
    pop si
    pop dx
    pop cx
    pop bx
    pop ax

    ret
music endp
```

 

引用

- [汇编语言（十四）8086汇编实现音乐的播放](https://blog.csdn.net/qq_40818798/article/details/84672262)
